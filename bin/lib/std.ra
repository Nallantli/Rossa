extern "libstd";

# BASIC

alloc := ((ref x) => alloc(x));

len := ((ref x) => len(x));

codes := ((ref x) => codes(x));

chars := ((ref x) => chars(x));

parse := ((ref x) => parse(x));

# OPERATOR LITERALS

`[]`(ref a, ref b) => _call_op 0 (a, b);
`..`(ref a, ref b) => _call_op 1 (a, b);
`..`(ref a, ref b, ref c) => _call_op 2 (a, b, c);
`<>`(ref a, ref b) => _call_op 3 (a, b);
`<>`(ref a, ref b, ref c) => _call_op 4 (a, b, c);

`+`(ref a, ref b) => _call_op 5 (a, b);
`-`(ref a, ref b) => _call_op 6 (a, b);
`*`(ref a, ref b) => _call_op 7 (a, b);
`/`(ref a, ref b) => _call_op 8 (a, b);
`%`(ref a, ref b) => _call_op 9 (a, b);
`**`(ref a, ref b) => _call_op 10 (a, b);

`<`(ref a, ref b) => _call_op 11 (a, b);
`>`(ref a, ref b) => _call_op 12 (a, b);
`<=`(ref a, ref b) => _call_op 13 (a, b);
`>=`(ref a, ref b) => _call_op 14 (a, b);

`|`(ref a, ref b) => _call_op 15 (a, b);
`^`(ref a, ref b) => _call_op 16 (a, b);
`&`(ref a, ref b) => _call_op 17 (a, b);
`<<`(ref a, ref b) => _call_op 18 (a, b);
`>>`(ref a, ref b) => _call_op 19 (a, b);

`++`(ref a, ref b) => _call_op 25 (a, b);

`()`(ref a, ref b : Array) => _call_op 20 (a, b);

`~`(ref a) => _call_op 21 (a);

`+`(ref a) => _call_op 22 (a);
`-`(ref a) => _call_op 23 (a);
`!`(ref a) => _call_op 24 (a);

`delete`(ref a, ref b) => _call_op 26 (a, b);

`@`(ref a) => _call_op 27 (a);

`==`(ref a, ref b) => a === b;
`!=`(ref a, ref b) => a !== b;

# CASTING

`->Number`(ref a) => a -> Number;
`->Boolean`(ref a) => a -> Boolean;
`->String`(ref a) => a -> String;
`->Array`(ref a) => a -> Array;
`->Dictionary`(ref a) => a -> Dictionary;
`->Type`(ref a) => a -> Type;

# CONSOLE

puts(ref s) => extern_call libstd._puts(s -> String);

puts(...) => {
	for s in _ do {
		extern_call libstd._puts(s -> String);
	}
}

putln(ref s) => extern_call libstd._puts((s -> String) ++ "\n");

putln(...) => {
	for s in _ do {
		extern_call libstd._puts(s -> String);
	}
	extern_call libstd._puts("\n");
}

putln() => puts("\n");

system(ref s : String) => extern_call libstd._system_call(s);

static cons {
	static decor {
		const FG_BLACK = "30";
		const FG_RED = "31";
		const FG_GREEN = "32";
		const FG_YELLOW = "33";
		const FG_BLUE = "34";
		const FG_MAGENTA = "35";
		const FG_CYAN = "36";
		const FG_WHITE = "37";
		const FG_BRIGHT_BLACK = "30;1";
		const FG_BRIGHT_RED = "31;1";
		const FG_BRIGHT_GREEN = "32;1";
		const FG_BRIGHT_YELLOW = "33;1";
		const FG_BRIGHT_BLUE = "34;1";
		const FG_BRIGHT_MAGENTA = "35;1";
		const FG_BRIGHT_CYAN = "36;1";
		const FG_BRIGHT_WHITE = "37;1";

		const BG_BLACK = "40";
		const BG_RED = "41";
		const BG_GREEN = "42";
		const BG_YELLOW = "43";
		const BG_BLUE = "44";
		const BG_MAGENTA = "45";
		const BG_CYAN = "46";
		const BG_WHITE = "47";
		const BG_BRIGHT_BLACK = "40;1";
		const BG_BRIGHT_RED = "41;1";
		const BG_BRIGHT_GREEN = "42;1";
		const BG_BRIGHT_YELLOW = "43;1";
		const BG_BRIGHT_BLUE = "44;1";
		const BG_BRIGHT_MAGENTA = "45;1";
		const BG_BRIGHT_CYAN = "46;1";
		const BG_BRIGHT_WHITE = "47;1";

		const BOLD = "1";
		const UNDERLINE = "4";
		const REVERSED = "7";

		const RESET = "0";
	}

	setDecor(ref s : String) => puts("\u001B[" ++ s ++ "m");

	setDecor(ref n : Number) => setDecor(n -> String);

	setDecor(ref v : Array) => {
		for c in v do {
			setDecor(c);
		}
	}

	setForeColor(ref r : Number, ref g : Number, ref b : Number) => {
		setDecor("38;5;" ++ ((16 + 36 * math.floor(5 * r / 255) + 6 * math.floor(5 * g / 255) + math.floor(5 * b / 255)) -> String));
	}

	setBackColor(ref r : Number, ref g : Number, ref b : Number) => {
		setDecor("48;5;" ++ ((16 + 36 * math.floor(5 * r / 255) + 6 * math.floor(5 * g / 255) + math.floor(5 * b / 255)) -> String));
	}

	moveCursor(ref x : Number, ref y : Number) => {
		s := "";
		if x < 0 then {
			s ++= "\u001B[" ++ (math.abs(x) -> String) ++ "D";
		} else {
			s ++= "\u001B[" ++ (x -> String) ++ "C";
		}
		if y < 0 then {
			s ++= "\u001B[" ++ (math.abs(y) -> String) ++ "A";
		} else {
			s ++= "\u001B[" ++ (x -> String) ++ "B";
		}
		puts(s);
	}

	setCursor(ref x : Number, ref y : Number) => puts("\u001B[" ++ (y -> String) ++ ";" ++ (x -> String) ++ "H");
}

static input {
	line() => extern_call libstd._input_line();

	token() => extern_call libstd._input_token();

	char() => extern_call libstd._input_char();
}

# RANDOM

struct Random {
	var ptr;

	init() => {
		ptr = (extern_call libstd._rand_init(clock.milliseconds()));
	}

	init(ref seed : Number) => {
		ptr = (extern_call libstd._rand_init(seed));
	}

	nextf() => extern_call libstd._rand_nextFloat(ptr, 0, 1);

	nextf(ref max : Number) => extern_call libstd._rand_nextFloat(ptr, 0, max);

	nextf(ref min : Number, ref max : Number) => extern_call libstd._rand_nextFloat(ptr, min, max);

	nexti() => extern_call libstd._rand_nextInt(ptr, 0, 1);

	nexti(ref max : Number) => extern_call libstd._rand_nextInt(ptr, 0, max);

	nexti(ref min : Number, ref max : Number) => extern_call libstd._rand_nextInt(ptr, min, max);

	nextb() => (nexti() -> Boolean);
}

# SYSTEM

exit() => extern_call libstd._exit(0);

exit(ref n : Number) => extern_call libstd._exit(n);

# TIME

static clock {
	milliseconds() => extern_call libstd._timeMS();

	sleep(ref ms : Number) => extern_call libstd._sleep(ms);

	format(ref s : String, ref time : Number) => extern_call libstd._clock_format(time, s);

	timeout(ref f : Function, ref ms : Number) => (new Thread(()[f, ms] => { sleep(ms); f(); })).detach();
}

# ARRAY

join(ref a : Array) => {
	s := "";
	for e in a do {
		s ++= (e -> String);
	}
	return s;
}

push(ref a : Array, ref value) => {
	refer a ++= [value];
}

unshift(ref a : Array, ref value) => {
	refer a = [value] ++ a;
}

pop(ref a : Array, ref count : Number) => {
	nv := alloc(len(a) - count);
	for i in 0 .. (len(a) - count) do {
		nv[i] = a[i];
	}
	refer a = nv;
}

shift(ref a : Array, ref count : Number) => {
	nv := alloc(len(a) - count);
	for i in count .. len(a) do {
		nv[i - count] = a[i];
	}
	refer a = nv;
}

tail(ref a : Array, ref count : Number) => {
	nv := a;
	return nv.shift(count);
}

tail(ref a : Array) => {
	return a.tail(1);
}

head(ref a : Array, ref count : Number) => {
	nv := a;
	return nv.pop(count);
}

head(ref a : Array) => {
	return a.pop(1);
}

pop(ref a : Array) => {
	refer pop(a, 1);
}

shift(ref a : Array) => {
	refer shift(a, 1);
}

map(ref a : Array, ref f : Function<Any>) => {
	nv := a;
	for e in nv do {
		e = f(e);
	}
	return nv;
}

map(ref a : Array, ref f : Function<Any, Number>) => {
	nv := a;
	for i in 0 .. nv.len() do {
		nv[i] = f(nv[i], i);
	}
	return nv;
}

filter(ref a : Array, ref f : Function<Any>) => {
	nv := [];
	for e in a do {
		if f(e) then {
			nv ++= [e];
		}
	}
	return nv;
}

filter(ref a : Array, ref f : Function<Any, Number>) => {
	nv := [];
	for i in 0 .. a.len() do {
		if f(a[i], i) then {
			nv ++= [a[i]];
		}
	}
	return nv;
}

erase(ref a : Array, ref value) => {
	nv := [];
	for e in a do {
		if e != value then {
			nv ++= [e];
		}
	}
	a = nv;
	refer a;
}

contains(ref a : Array, ref value) => {
	for e in a do {
		if e == value then {
			return true;
		}
	}
	return false;
}

reverse(ref a : Array) => {
	nv := alloc(len(a));
	for i in 0 .. len(a) do {
		nv[i] = a[len(a) - i - 1];
	}
	return nv;
}

back(ref a : Array) => {
	refer a[len(a) - 1];
}

empty(ref a : Array) => {
	return len(a) == 0;
}

sort(ref a : Array, ref comp : Function<Any, Any>) => {
	if a.len() < 2 then {
		return a;
	}

	left := [];
	right := [];
	pivot := a[0];
	for i in 1 .. a.len() do {
		if comp(a[i], pivot) then {
			left.push(a[i]);
		} else {
			right.push(a[i]);
		}
	}

	return sort(left) ++ [ pivot ] ++ sort(right);
}

sort(ref a : Array) => {
	return a.sort(`<`);
}

first(ref a : Array) => {
	refer a[0];
}

second(ref a : Array) => {
	refer a[1];
}

# STRING

push(ref s : String, ref value : Number) => {
	refer s ++= chars(value);
}

unshift(ref s : String, ref value : Number) => {
	refer s = chars(value) ++ s;
}

pop(ref s : String, ref count : Number) => {
	refer s = (s -> Array).pop(count).join();
}

shift(ref s : String, ref count : Number) => {
	refer s = (s -> Array).shift(count).join();
}

pop(ref s : String) => {
	refer s = pop(s, 1);
}

shift(ref s : String) => {
	refer s = shift(s, 1);
}

reverse(ref s : String) => {
	return (s -> Array).reverse().join();
}

empty(ref s : String) => {
	return len(s) == 0;
}

`[]`(ref s : String, ref i : Number) => {
	return (s -> Array)[i];
}

`..`(ref a : String, ref b : String) => {
	return [codes(a)[0] .. codes(b)[0]];
}

parseDict(ref a : String) => {
	return parse(a ++ ";")[0];
}

size(ref a : String) => extern_call libstd._string_size(a);

split(ref s : String, ref c : Number) => s.split(chars(c));

split(ref s : String, ref d : String) => {
	if d.len() != 1 then {
		throw "Cannot split a String where the delimiter's length is not 1";
	}

	a := (s -> Array) ++ [d];
	c := "";
	r := [];

	for e in a do {
		if e == d then {
			r ++= [c];
			c = "";
		} else {
			c ++= e;
		}
	}

	return r;
}

# MATH

static math {
	const EPSILON = (|> {
		eps := 1;
		while (1 + eps / 2) != 1 do {
			eps /= 2;
		}
		return eps;
	})();

	const PI = 3.14159265359;

	rand() => extern_call libstd._math_rand();

	srand(ref x : Number) => extern_call libstd._math_srand(x);

	log(ref x : Number) => extern_call libstd._log(x);

	abs(ref x : Number) => (x < 0 ? -x : x);

	floor(ref x : Number) => extern_call libstd._floor(x);

	ceil(ref x : Number) => extern_call libstd._ceil(x);

	round(ref x : Number) => extern_call libstd._round(x);

	round(ref x : Number, ref place : Number) => (extern_call libstd._round(x * (10 ** place))) / (10 ** place);

	min(ref a : Number, ref b : Number) => (a < b ? a : b);

	max(ref a : Number, ref b : Number) => (a > b ? a : b);

	sin(ref x : Number) => extern_call libstd._sin(x);

	cos(ref x : Number) => extern_call libstd._cos(x);

	tan(ref x : Number) => extern_call libstd._tan(x);

	csc(ref x : Number) => 1 / sin(x);

	sec(ref x : Number) => 1 / cos(x);

	cot(ref x : Number) => 1 / tan(x);

	asin(ref x : Number) => extern_call libstd._asin(x);

	acos(ref x : Number) => extern_call libstd._acos(x);

	atan(ref x : Number) => extern_call libstd._atan(x);

	sinh(ref x : Number) => extern_call libstd._sinh(x);

	cosh(ref x : Number) => extern_call libstd._cosh(x);

	tanh(ref x : Number) => extern_call libstd._tanh(x);

	csch(ref x : Number) => 1 / sinh(x);

	sech(ref x : Number) => 1 / cosh(x);

	coth(ref x : Number) => 1 / tanh(x);

	asinh(ref x : Number) => extern_call libstd._asinh(x);

	acosh(ref x : Number) => extern_call libstd._acosh(x);

	atanh(ref x : Number) => extern_call libstd._atanh(x);
}

static regex {
	match(ref s : String, ref r : String) => extern_call libstd._regex_match(r, s);

	replace(ref s : String, ref r : String, ref x : String) => extern_call libstd._regex_replace(r, x, s);
}

struct Thread {
	var ptr;

	init(ref f : Function) => {
		ptr = (extern_call libstd._thread_init(f));
	}

	join() => extern_call libstd._thread_join(ptr);

	detach() => extern_call libstd._thread_detach(ptr);

	rem() => {
		detach();
	}
}

split(ref f : Function) => extern_call libstd._function_split(f);

`+`(ref f : Function, ref g : Function<Any>) => {
	return (...)[f, g] => g(`()`(f, _));
}